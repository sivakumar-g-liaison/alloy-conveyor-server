/*
 * Copyright 2013 Liaison Technologies
 *
 *      Licensed under the Apache License, Version 2.0 (the "License");
 *      you may not use this file except in compliance with the License.
 *      You may obtain a copy of the License at
 *
 *          http://www.apache.org/licenses/LICENSE-2.0
 *
 *      Unless required by applicable law or agreed to in writing, software
 *      distributed under the License is distributed on an "AS IS" BASIS,
 *      WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *      See the License for the specific language governing permissions and
 *      limitations under the License.
 */

// Establish version and status
ext.githubProjectName = rootProject.name // Change if github project name is not the same as the root project's name

buildscript {
    repositories {
        mavenCentral() // maven { url 'http://jcenter.bintray.com' }
    }
    apply from: file('gradle/buildscript.gradle'), to: buildscript 
}

allprojects {
    group = 'com.liaison.nucleus'
    repositories { 
        mavenCentral() // maven { url: 'http://jcenter.bintray.com' }

        maven { url releasesArtifactRepoURL }
        maven { url snapshotsArtifactRepoURL }
	maven { url thirdpartyArtifactRepoURL }
    }
    
    configurations {
        all {
            // check for updates every build
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        }
    }    
}

apply from: file('gradle/convention.gradle')
apply from: file('gradle/maven.gradle')
apply from: file('gradle/check.gradle')
//apply from: file('gradle/license.gradle')
//apply from: file('gradle/release.gradle')
apply from: file("gradle/bundler/bundle.gradle")
apply from: file("gradle/bundler/framework.gradle")
apply from: file("gradle/bundler_project.gradle")

war {
	//a hack to take the empty war of project_root of the mix
	archiveName=".ignore_me_seriously_thanks"
}

subprojects {
    test {
        // workaround for Java7 and Cobutura/Emma instrumentation problem
        jvmArgs '-XX:-UseSplitVerifier'    
    }
}

/*
 * karyon-admin provides rest services that expose administrative data
 * like configuration properties, classes/jars loaded, jmx properties, etc
 *
 */
project(':karyon-admin') {
    dependencies {
        compile "com.netflix.karyon:karyon-core:${karyonVersion}"
        compile 'javax.servlet:servlet-api:2.5'
        compile 'org.eclipse.jetty:jetty-server:7.6.7.v20120910'
        compile 'org.eclipse.jetty:jetty-servlet:7.6.7.v20120910'
        compile 'com.sun.jersey.contribs:jersey-guice:1.8'
        compile 'com.google.inject.extensions:guice-servlet:3.0'
        compile 'com.netflix.pytheas:pytheas-core:1.11'
        testCompile 'org.testng:testng:6.8'
        testCompile 'junit:junit:4.10'
        runtime 'org.codehaus.jackson:jackson-mapper-asl:1.9.11'
    }
    test {
        // com.netflix.adminresources.AdminResourceTest fails intermitantly
        exclude  'com/netflix/adminresources/**'
    }    
}

/*
 * karyon-admin-web puts a pretty face (webapp) on karyon-admin
 */
project(':karyon-admin-web') {
    dependencies {
        compile     project(':karyon-admin')
        compile 'com.google.code.gson:gson:2.1'
        compile 'com.netflix.pytheas:pytheas-core:1.11'
        testCompile 'org.testng:testng:6.8'
        testCompile 'junit:junit:4.10'
        runtime 'org.freemarker:freemarker:2.3.15'
        runtime 'org.codehaus.jackson:jackson-mapper-asl:1.9.11'
    }
}

